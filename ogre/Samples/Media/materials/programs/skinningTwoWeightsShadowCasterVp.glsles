#version 100

precision highp float;
precision highp int;
precision lowp sampler2D;
precision lowp samplerCube;

// Example GLSL program for skinning with two bone weights per vertex

attribute vec4 vertex;
attribute vec4 uv0;
attribute vec4 blendIndices;
attribute vec4 blendWeights;
varying vec4 colour;

// 3x4 matrix, passed as vec4's for compatibility with GL 2.0
// GL 2.0 supports 3x4 matrices
// Support 24 bones ie 24*3, but use 72 since our parser can pick that out for sizing
uniform vec4 worldMatrix3x4Array[72];
uniform mat4 viewProjectionMatrix;
uniform vec4 ambient;
uniform sampler2D skinTex;

void main()
{
	vec3 blendPos = vec3(0.0, 0.0, 0.0);

	for (int bone = 0; bone < 2; ++bone)
	{
		// Perform matrix multiplication manually since no 3x4 matrices
	    int idx = int(blendIndices[bone]) * 3;

        // Unroll the loop manually
		mat4 worldMatrix;
		worldMatrix[0] = worldMatrix3x4Array[idx];
		worldMatrix[1] = worldMatrix3x4Array[idx + 1];
		worldMatrix[2] = worldMatrix3x4Array[idx + 2];
		worldMatrix[3] = vec4(0.0);
		// Now weight this into final 
		blendPos += (vertex * worldMatrix).xyz * blendWeights[bone];
	}

	// Apply view / projection to position
	gl_Position = viewProjectionMatrix * vec4(blendPos, 1.0);
	
//	gl_FrontSecondaryColor = vec4(0,0,0,0);
	vec4 texColour = texture2D(skinTex, uv0.xy);
	colour = ambient + texColour;
//	gl_TexCoord[0] = uv0;
}
